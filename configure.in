dnl ##
dnl ## Process this file with autoconf to produce a configure script.
dnl ## 
AC_PREREQ([2.68])
AC_INIT([ANU Nilpotent Quotient Program], [2.2cvs], [support@gap-system.org], [nq])

AC_CONFIG_AUX_DIR(cnf)

AC_CONFIG_HEADER(src/config.h:src/config.hin)

dnl ##
dnl ## C is the language 
dnl ##
AC_LANG_C

dnl ##
dnl ## determine the system name
dnl ##
AC_CANONICAL_SYSTEM

dnl ##
dnl ## Checks for programs.
dnl ##
AC_PROG_AWK
AC_PROG_CC
AC_PROG_MAKE_SET

dnl ##
dnl ## Checks for system header files.
dnl ##
AC_CHECK_HEADERS([stdlib.h string.h sys/time.h])

dnl ##
dnl ## Locate the GAP root dir
dnl ##
dnl ## If no directory is specified, then this defaults to ../..
AC_ARG_WITH([gap-root],
	[AS_HELP_STRING([--with-gap-root=DIR],
		[directory where GAP is installed @<:@default=../..@:>@])],
	[GAP_ROOT="$with_gap_root" ; GAP_ROOT_real="$with_gap_root"; GAP_ROOT_display="$with_gap_root"],
	[GAP_ROOT='${top_srcdir}/../..' ; GAP_ROOT_real="${srcdir}/../.."; GAP_ROOT_display=../..])
AC_MSG_CHECKING(for path to GAP installation)
SYSINFO_GAP=$GAP_ROOT_real/sysinfo.gap
if test -f $SYSINFO_GAP ; then
	AC_MSG_RESULT([$GAP_ROOT_display])
else
	AC_MSG_ERROR([$GAP_ROOT_display/sysinfo.gap not found])
fi
AC_SUBST(GAP_ROOT)

dnl ##
dnl ## Parse the GAParch from sysinfo.gap
dnl ##
AC_MSG_CHECKING(for active GAP architecture)
GAP_ARCH=`fgrep "GAParch=" $SYSINFO_GAP | sed -e 's/^GAParch=\(.*\)$/\1/'`
if test x$GAP_ARCH = x ; then
	AC_MSG_ERROR([GAParch not found in $GAP_ROOT_display/sysinfo.gap])
else
	AC_MSG_RESULT([$GAP_ARCH])
fi
AC_SUBST(GAP_ARCH)

dnl ##
dnl ## Check for GMP
dnl ##

AC_CHECK_HEADER( gmp.h, HAVE_GMP_INC=true, HAVE_GMP_INC=false)
if test $HAVE_GMP_INC = false; then
   echo ""; 
   echo "    Warning: cannot locate the GNU MP include file gmp.h.";
   echo "    Find the directory containing  gmp.h  and when you";
   echo "    run make add the argument GNU_MP_INC=<gnu-mp-dir>.";
   echo ""
fi

dnl ##
dnl ## Checks for existence of GMP libraries and include files.
dnl ## The first check is for GMP 4, the second for GMP 3 or earlier. 
dnl ##
AC_CHECK_LIB( gmp, __gmpz_init, HAVE_GMP=true, HAVE_GMP=false)
if test $HAVE_GMP = false; then
   AC_CHECK_LIB( gmp, mpz_out_str, HAVE_GMP=true, HAVE_GMP=false)
fi;
if test $HAVE_GMP = false; then
   echo ""; 
   echo "    Warning: cannot locate the GNU MP library libgmp.a.";
   echo "    Find the directory containing  libgmp.a  and when you run";
   echo "    make add the argument GNU_MP_LIB=<gnu-mp-dir>.";
   echo ""
fi

dnl ##
dnl ## Checks for typedefs, structures, and compiler characteristics.
dnl ##
AC_TYPE_LONG_LONG_INT

dnl ##
dnl ## Checks for library functions.
dnl ##
AC_FUNC_MALLOC
AC_FUNC_REALLOC
AC_CHECK_FUNCS([gethostname getrusage])

AC_CONFIG_FILES([Makefile doc/Makefile examples/Makefile src/Makefile])
AC_OUTPUT
